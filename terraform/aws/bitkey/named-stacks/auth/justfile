[private]
default:
  just --list

download_artifacts:
  #!/usr/bin/env bash
  set -ex
  GITHUB_BRANCH=main
  GITHUB_RUN_ID=$(gh run list -R squareup/wallet -w "Auth Lambdas" -b ${GITHUB_BRANCH} --json 'conclusion,databaseId,number,startedAt,status' | jq -r 'map(select(.conclusion == "success"))[0].databaseId')
  if ! [[ $GITHUB_RUN_ID =~ ^[0-9]+$ ]] ; then
     echo "Failed to get latest successful Github run, got: \"${GITHUB_RUN_ID}\""; exit 1
  fi
  (
    rm -rf assets
    mkdir -p assets
    cd assets
    gh run download -R squareup/wallet $GITHUB_RUN_ID -p '*_auth_challenge-lambda' -n 'pre_token_generation-lambda'
  )

_terragrunt command:
  terragrunt {{command}} \
    -var define_auth_challenge_asset_dir=./assets/define_auth_challenge-lambda \
    -var create_auth_challenge_asset_dir=./assets/create_auth_challenge-lambda \
    -var verify_auth_challenge_asset_dir=./assets/verify_auth_challenge-lambda

init: (_terragrunt "init")
plan: (_terragrunt "plan")
apply: (_terragrunt "apply")
